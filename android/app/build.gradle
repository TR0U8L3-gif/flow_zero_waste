plugins {
    id "com.android.application"
    // START: FlutterFire Configuration
    id 'com.google.gms.google-services'
    // END: FlutterFire Configuration
    id "kotlin-android"
    // The Flutter Gradle Plugin must be applied after the Android and Kotlin Gradle plugins.
    id "dev.flutter.flutter-gradle-plugin"
}

dependencies {
    implementation("com.google.android.material:material:1.12.0")
    implementation(platform("com.google.firebase:firebase-bom:33.5.1"))
    implementation("com.google.firebase:firebase-analytics")
    implementation("androidx.annotation:annotation:1.9.0")
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.tr0u8l3gif.app"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = "25.1.8937393"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId = "com.tr0u8l3gif.flow.app"
        // You can update the following values to match your application needs.
        // For more information, see: https://flutter.dev/to/review-gradle-config.
        minSdk = 21
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }

    signingConfigs {
        release {
            keyAlias = keystoreProperties['keyAlias']
            keyPassword = keystoreProperties['keyPassword']
            storeFile = keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword = keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig = signingConfigs.debug
            // signingConfig = signingConfigs.release
        }
    }

    flavorDimensions "default"
    productFlavors { 
        development {
            dimension "default"
            manifestPlaceholders = [appName: "[DEV] Flow - Zero Waste App"]
            resValue "string", "app_name", "[DEV] Flow - Zero Waste App"
            applicationIdSuffix ".dev"
        }
        demonstration {
            dimension "default"
            manifestPlaceholders = [appName: "[DEMO] Flow - Zero Waste App"]
            resValue "string", "app_name", "[DEMO] Flow - Zero Waste App"
            applicationIdSuffix ".demo"
        }
        production {
            dimension "default"
            manifestPlaceholders = [appName: "Flow - Zero Waste App"]
            resValue "string", "app_name", "Flow - Zero Waste App"
            applicationIdSuffix ""
        }
    }


}

flutter {
    source = "../.."
}
